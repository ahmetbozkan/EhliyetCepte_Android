plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk buildConfig.compileSdkVersion

    defaultConfig {
        applicationId buildConfig.applicationId
        minSdk buildConfig.minSdkVersion
        targetSdk buildConfig.targetSdkVersion
        versionCode generateVersionCode()
        versionName generateVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
    }
}

dependencies {
    // navigation component
    implementation deps.androidx.navigation

    // dagger - hilt
    implementation deps.dagger.hilt
    kapt deps.dagger.kapt

    // lifecycle
    implementation deps.androidx.lifecycle.fragment
    implementation deps.androidx.lifecycle.viewmodel
    implementation deps.androidx.lifecycle.livedata
    implementation deps.androidx.lifecycle.activity

    // coroutines
    implementation deps.kotlin.coroutines

    // room database
    implementation deps.room.runtime
    implementation deps.room.ktx
    kapt deps.room.kapt

    // datastore
    implementation deps.datastore

    // glide
    implementation deps.glide

    implementation deps.kotlin.ktx
    implementation deps.androidx.appcompat
    implementation deps.material
    implementation deps.androidx.constraintlayout
    testImplementation 'junit:junit:4.+'
    androidTestImplementation deps.androidx.junit
    androidTestImplementation deps.androidx.espresso
}

private Integer generateVersionCode() {
    return buildConfig.version.major * 10000 + buildConfig.version.minor * 100 + buildConfig.version.patch
}

private String generateVersionName() {
    return "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
}